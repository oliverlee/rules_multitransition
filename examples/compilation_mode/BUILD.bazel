load("@local_workspace_directory//:defs.bzl", "BAZEL_WORKSPACE_ROOT")
load("@rules_cc//cc:defs.bzl", "cc_test")
load(
    "@rules_multitransition//:defs.bzl",
    "multitransition_test",
    "transition_config",
)
load("@rules_shell//shell:sh_test.bzl", "sh_test")

multitransition_test(
    cc_test,
    name = "compilation_mode_test",
    srcs = ["dummy.cpp"],
    transitions = [
        transition_config(
            name = "dbg",
            compilation_mode = "dbg",
        ),
        transition_config(
            name = "opt",
            compilation_mode = "opt",
        ),
    ],
)

[
    sh_test(
        name = "uses_{}_compile_flags_test".format(flavor),
        srcs = ["check_flag.bash"],
        args = [
            BAZEL_WORKSPACE_ROOT,
            "//{}:compilation_mode_test.{}".format(
                package_name(),
                flavor,
            ),
            flag,
        ],
        env_inherit = [
            "HOME",
        ],
        tags = ["local"],
    )
    for flavor, flag in [
        ("dbg", "-g"),
        ("opt", "-O2"),
    ]
]
