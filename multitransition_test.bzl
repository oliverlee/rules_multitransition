"""
Implementation of 'multitransition_test' macro.
"""

load("//:transition_config.bzl", "is_transition_config")

visibility("private")

def _append_if_missing(values, item):
    """
    Appends an element to a list if not already present.
    """
    if item in values:
        return values
    return values + [item]

def multitransition_test(
        test_rule,
        *,
        name,
        transitions,
        tags = [],
        timeout = None,
        size = None,
        target_compatible_with = [],
        **kwargs):
    """
    Defines a test and additional variants using the specified transitions.

    Args:
      test_rule: rule
        Underlying test rule to use.
      name: string
        Name for the underlying test target. Additional test varaints use this
        as the base name.
      transitions: list of 'transition_config'
        Configurations for each variant test target.
      tags: string_list
        Tags to apply to all test targets.
      timeout: string
        Applies to all test targets.
      size: string
        Applies to all test targets.
      target_compatible_with: label_list
        Use of this attr is not permitted.
      **kwargs:
        Additional keyword arguments to pass to the test rule.

    Creates a test target along with test target variants, with one variant per
    item in 'transitions'. All variants are aggregated with a 'test_suite'
    target.
    """
    if target_compatible_with:
        fail("'target_compatible_with' is not settable with 'multitransition_test'")
    tags_with_manual = _append_if_missing(tags, "manual")

    test_rule(
        name = name,
        tags = tags,
        timeout = timeout,
        size = size,
        target_compatible_with = select({
            Label("//config:multitransition_test_enabled"): [
                Label("@platforms//:incompatible"),
            ],
            Label("//config:multitransition_test_disabled"): [],
        }),
        **kwargs
    )

    test_rule(
        name = name + ".base",
        tags = tags_with_manual,
        timeout = timeout,
        size = size,
        target_compatible_with = select({
            Label("//config:multitransition_test_enabled"): [],
            Label("//config:multitransition_test_disabled"): [
                Label("@platforms//:incompatible"),
            ],
        }),
        **kwargs
    )

    tests = []
    for i, config in enumerate(transitions):
        if not is_transition_config(config):
            fail(
                "Invalid transition config: '{}'".format(config) +
                "\nUse 'transition_config' or a closure generated by " +
                "'make_transition_config'.",
            )

        transitioned_test_name = name + "." + (config.name or str(i))
        tests.append(transitioned_test_name)
        config.test_rule(
            **({
                "name": transitioned_test_name,
                "src": name + ".base",
                "tags": tags,
                "timeout": timeout,
                "size": size,
                "provided_attrs": config.attrs.keys(),
                "target_compatible_with": select({
                    Label("//config:multitransition_test_enabled"): [],
                    Label("//config:multitransition_test_disabled"): [
                        Label("@platforms//:incompatible"),
                    ],
                }),
            } | config.attrs)
        )

    native.test_suite(
        name = name + ".suite",
        tests = tests,
        tags = tags_with_manual,
    )
